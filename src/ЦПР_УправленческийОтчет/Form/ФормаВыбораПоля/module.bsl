
////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Функция возвращает Истина если тип значения содержит один из допустимых типов.
//
Функция СодержитДопустимыйТип(ТипЗначения)
	
	Для Каждого ДопустимыйТип Из ДопустимыеТипы.Типы() Цикл
		
		Если ТипЗначения.СодержитТип(ДопустимыйТип) Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции // СодержитДопустимыйТип()

// Функция определяет возможность выбора значения. Если возможность есть, возвращает Истина.
//
Функция ОпределитьВыбранноеЗначение()
	
	ТекущиеДанные = ЭлементыФормы.Поля.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Определение наличия в выбранном поле хотя бы одного допустимого типа
	Если НЕ СодержитДопустимыйТип(ТекущиеДанные.ТипЗначения) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Установка выбранного значения
	Если ТипЗнч(ВладелецФормы) = Тип("ПолеВвода") Тогда
		ВладелецФормы.Значение = ТекущиеДанные.ПутьКДанным;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции // ОпределитьВыбранноеЗначение()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПриОткрытии" формы.
//
Процедура ПриОткрытии()
	
	Построитель.ЗаполнитьНастройки();
	
КонецПроцедуры // ПриОткрытии()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ НАЖАТИЯ КНОПОК ОСНОВНОЙ КОМАНДНОЙ ПАНЕЛИ ФОРМЫ

// Процедура - обработчик нажатия кнопки "ОК" основной командной панели формы.
//
Процедура ОсновныеДействияФормыОК(Кнопка)
	
	Если ОпределитьВыбранноеЗначение() Тогда
		Закрыть(Истина);
	КонецЕсли;
	
КонецПроцедуры // ОсновныеДействияФормыОК()

// Процедура - обработчик нажатия кнопки "Отмена" основной командной панели формы.
//
Процедура ОсновныеДействияФормыОтмена(Кнопка)
	
	Закрыть(Ложь);
	
КонецПроцедуры // ОсновныеДействияФормыОтмена()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

// Процедура - обработчик события "Выбор" элемента формы "Поля".
//
Процедура ПоляВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Если ОпределитьВыбранноеЗначение() Тогда
		Закрыть(Истина);
	КонецЕсли;
	
КонецПроцедуры // ПоляВыбор()

// Процедура - обработчик события "ПриПолученииДанных" элемента формы "Поля".
//
Процедура ПоляПриПолученииДанных(Элемент, ОформленияСтрок)
	
	Для Каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		
		Если СодержитДопустимыйТип(ОформлениеСтроки.ДанныеСтроки.ТипЗначения) Тогда
			Если ОформлениеСтроки.ДанныеСтроки.Родитель = Неопределено Тогда
				ИндексКартинки = 2;
			Иначе
				ИндексКартинки = 4;
			КонецЕсли;
		Иначе
			ИндексКартинки = 0;
		КонецЕсли;
		
		ОформлениеСтроки.Ячейки.Поле.ИндексКартинки = ИндексКартинки;
		ОформлениеСтроки.Ячейки.Поле.ОтображатьКартинку = Истина;;
		
	КонецЦикла;
	
КонецПроцедуры // ПоляПриПолученииДанных()




