Перем НеИспользуемыйЦвет;
Перем ФиктивнаяСтрокаГруппировка;

Процедура ОсновныеДействияФормыОК(Кнопка)
	
	ВыборПоля();
	
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если РодителиПоля = Неопределено Тогда
		РодителиПоля = Новый Массив;
	КонецЕсли;
	
	ЭлементыФормы.НастройкиКомпоновщика.ПодробнаяНастройка = Истина;
	ФиктивнаяСтрокаГруппировка = КомпоновщикНастроек.Настройки.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	ЭлементыФормы.НастройкиКомпоновщика.ТекущаяСтрока = ФиктивнаяСтрокаГруппировка;
	
	Если Режим = "Отбор" Тогда
		ЭлементыФормы.ПанельОсновная.ТекущаяСтраница = ЭлементыФормы.ПанельОсновная.Страницы.Отбор;
		Заголовок = "Выбор поля отбора";
	ИначеЕсли Режим = "Дата" Тогда
		ЭлементыФормы.ПанельОсновная.ТекущаяСтраница = ЭлементыФормы.ПанельОсновная.Страницы.ПоляГруппировки;
		Заголовок = "Выбор поля (дата)";
		Поле = Новый ПолеКомпоновкиДанных("UserFields");
		Ограничение = ЭлементыФормы.Выбор.ОграниченияИспользования.Добавить();
		Ограничение.Доступность = Ложь;
		Ограничение.Поле = Поле;
	ИначеЕсли Режим = "ЧислоИСтрока" Тогда
		ЭлементыФормы.ПанельОсновная.ТекущаяСтраница = ЭлементыФормы.ПанельОсновная.Страницы.Выбор;
		Заголовок = "Выбор поля (строка или число)";
		Поле = Новый ПолеКомпоновкиДанных("UserFields");
		Ограничение = ЭлементыФормы.Выбор.ОграниченияИспользования.Добавить();
		Ограничение.Доступность = Ложь;
		Ограничение.Поле = Поле;
	ИначеЕсли Режим = "Все" Тогда
		ЭлементыФормы.ПанельОсновная.ТекущаяСтраница = ЭлементыФормы.ПанельОсновная.Страницы.Выбор;
		Заголовок = "Выбор поля";
		Поле = Новый ПолеКомпоновкиДанных("UserFields");
		Ограничение = ЭлементыФормы.Выбор.ОграниченияИспользования.Добавить();
		Ограничение.Доступность = Ложь;
		Ограничение.Поле = Поле;
	Иначе
		Для каждого РодительскоеПоле Из РодителиПоля Цикл
			Поле = Новый ПолеКомпоновкиДанных(РодительскоеПоле.Поле);
			Ограничение = ЭлементыФормы.ПоляГруппировки.ОграниченияИспользования.Добавить();
			Ограничение.Доступность = Ложь;
			Ограничение.Поле = Поле;
		КонецЦикла;
		ЭлементыФормы.ПанельОсновная.ТекущаяСтраница = ЭлементыФормы.ПанельОсновная.Страницы.ПоляГруппировки;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	КомпоновщикНастроек.Настройки.Структура.Удалить(ФиктивнаяСтрокаГруппировка);
	
КонецПроцедуры

Процедура ДоступныеПоляВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Не ВыборПоля();
	
КонецПроцедуры

Процедура ПоляГруппировкиВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Не ВыборПоля();
	
КонецПроцедуры

Функция ВыборПоля()
	
	Если Режим = "Отбор" Тогда
		ТекущиеДанные = ЭлементыФормы.Отбор.ТекущиеДанные;
	ИначеЕсли Режим = "ЧислоИСтрока" ИЛИ Режим = "Все" Тогда
		ТекущиеДанные = ЭлементыФормы.Выбор.ТекущиеДанные;
	ИначеЕсли Режим = "Дата" Тогда
		ТекущиеДанные = ЭлементыФормы.ПоляГруппировки.ТекущиеДанные;
	Иначе
		ТекущиеДанные = ЭлементыФормы.ПоляГруппировки.ТекущиеДанные;
	КонецЕсли;
	
	Если ТекущиеДанные = Неопределено ИЛИ ТекущиеДанные.Папка Тогда
		Возврат Ложь;
	КонецЕсли;
	Если Режим = "ЧислоИСтрока" ИЛИ Режим = "Дата" Тогда
		ТипЗначенияПоля = КомпоновщикНастроек.Настройки.ДоступныеПоляВыбора.НайтиПоле(Новый ПолеКомпоновкиДанных(ТекущиеДанные.Поле)).Тип;
		Если Режим = "ЧислоИСтрока" Тогда
			Если ТипЗначенияПоля.Типы().Найти(Тип("Строка")) <> Неопределено ИЛИ ТипЗначенияПоля.Типы().Найти(Тип("Число")) <> Неопределено Тогда
				Закрыть(ТекущиеДанные);
			КонецЕсли;
		ИначеЕсли Режим = "Дата" Тогда
			Если ТипЗначенияПоля.Типы().Найти(Тип("Дата")) <> Неопределено Тогда
				Закрыть(ТекущиеДанные);
			КонецЕсли;
		КонецЕсли;
	Иначе
		Закрыть(ТекущиеДанные);
	КонецЕсли;
	Возврат Истина;

КонецФункции

Процедура ДоступныеПоляПриПолученииДанных(Элемент, ОформленияСтрок)
	
	Если Режим <> "ЧислоИСтрока" И Режим <> "Дата" Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		ТипЗначенияПоля = КомпоновщикНастроек.Настройки.ДоступныеПоляВыбора.НайтиПоле(Новый ПолеКомпоновкиДанных(ОформлениеСтроки.ДанныеСтроки.Поле)).Тип;
		Если Режим = "ЧислоИСтрока" Тогда
			Если ТипЗначенияПоля.Типы().Найти(Тип("Строка")) <> Неопределено ИЛИ ТипЗначенияПоля.Типы().Найти(Тип("Число")) <> Неопределено Тогда
				ОформлениеСтроки.ЦветТекста = Новый Цвет;	
			Иначе
				ОформлениеСтроки.ЦветТекста = НеИспользуемыйЦвет;
			КонецЕсли;
		ИначеЕсли Режим = "Дата" Тогда
			Если ТипЗначенияПоля.Типы().Найти(Тип("Дата")) <> Неопределено Тогда
				ОформлениеСтроки.ЦветТекста = Новый Цвет;	
			Иначе
				ОформлениеСтроки.ЦветТекста = НеИспользуемыйЦвет;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

НеИспользуемыйЦвет = Новый Цвет(153, 153, 153);


